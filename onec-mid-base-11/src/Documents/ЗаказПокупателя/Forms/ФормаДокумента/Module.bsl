
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// ++ Забиров Т. Р.: Вывод элемента ДЗ_КонтактноеЛицо на форму 
	ЭлементДЗ_КонтактноеЛицо = Элементы.Добавить("ЭлементДЗ_КонтактноеЛицо", Тип("ПолеФормы"),
		Элементы.ГруппаШапкаПраво);
	ЭлементДЗ_КонтактноеЛицо.ПутьКДанным = "Объект.ДЗ_КонтактноеЛицо";
	ЭлементДЗ_КонтактноеЛицо.Вид = ВидПоляФормы.ПолеВвода; 
	// --
	
	// ++ Забиров Т. Р.: Добавление группы для Элемента ДЗ_СогласованнаяСкидка и команды применения скидки
	ГруппаСкидка = Элементы.Добавить("ГруппаСкидка", Тип("ГруппаФормы"),Элементы.ГруппаШапкаЛево); 
	ГруппаСкидка.Вид = ВидГруппыФормы.ОбычнаяГруппа; 
	ГруппаСкидка.ОтображатьЗаголовок = Ложь;
	
	// Вывод элемента ДЗ_СогласованнаяСкидка на форму 
	ЭлементДЗ_СогласованнаяСкидка = Элементы.Добавить("ЭлементДЗ_СогласованнаяСкидка",Тип("ПолеФормы"),Элементы.ГруппаСкидка); 
	ЭлементДЗ_СогласованнаяСкидка.ПутьКДанным = "Объект.ДЗ_СогласованнаяСкидка";
	ЭлементДЗ_СогласованнаяСкидка.Вид = ВидПоляФормы.ПолеВвода; 
	ЭлементДЗ_СогласованнаяСкидка.УстановитьДействие("ПриИзменении", "ДЗ_СогласованнаяСкидкаПриИзменении"); 	
	
	// Добавление команды применения скидки
	Команда = Команды.Добавить("ПрименитьСкидку");
	Команда.Заголовок = "Применить скидку";
	Команда.Действие = "КомандаПрименитьСкидку"; 
	
	КнопкаФормы = Элементы.Добавить("КнопкаПрименитьСкидку", Тип("КнопкаФормы"),Элементы.ГруппаСкидка); 
	КнопкаФормы.ИмяКоманды = ("ПрименитьСкидку");
	КнопкаФормы.Вид = ВидКнопкиФормы.ОбычнаяКнопка;
	КнопкаФормы.Картинка = БиблиотекаКартинок.Обновить;
	КнопкаФормы.Отображение = ОтображениеКнопки.КартинкаИТекст;
	// --
		
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
    ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДЗ_СогласованнаяСкидкаПриИзменении(Элемент) 
	
	// ++ Забиров Т. Р.: Вопрос о необходимости применения скидки при ее ввводе   
	Если Объект.Товары.Количество() = 0 И Объект.Услуги.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;    
	
	ВопросПользователю();   
	// --
		
КонецПроцедуры
#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриИзменении(Элемент)
	РассчитатьСуммуДокумента();
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСкидкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыУслуги

&НаКлиенте
Процедура УслугиКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиСкидкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиПриИзменении(Элемент)
	РассчитатьСуммуДокумента();
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаПрименитьСкидку(Команда)
	
	// ++ Забиров Т. Р.: Добовление команды применения скидки
	ПрименитьСкидку();
	// --
	
КонецПроцедуры

#КонецОбласти



#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура РассчитатьСуммуСтроки(ТекущиеДанные)
	
	// ++ Забиров Т. Р.: Учитывать общую скидку при расчете суммы 
	//КоэффициентСкидки = 1 - ТекущиеДанные.Скидка / 100;
	//ТекущиеДанные.Сумма = ТекущиеДанные.Цена * ТекущиеДанные.Количество * КоэффициентСкидки; 
	ОпределениеРазмераСкидкиИПересчетСтрок(ТекущиеДанные);
	// --

КонецПроцедуры

&НаКлиенте
Процедура ОпределениеРазмераСкидкиИПересчетСтрок(ТекущиеДанные)
	
	// ++ Забиров Т. Р.: определение общего размера скидки и пересчет сумм строк
	ОбщаяСкидка = ТекущиеДанные.Скидка + Объект.ДЗ_СогласованнаяСкидка;
	Если ОбщаяСкидка > 100 Тогда
		ТекущиеДанные.Сумма = 0; 
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = "Скидка не может превышать 100%";
		Сообщение.Сообщить();
	Иначе
		ТекущиеДанные.Сумма = ТекущиеДанные.Цена * ТекущиеДанные.Количество * (1 - ОбщаяСкидка / 100);
	КонецЕсли;
	// --
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСуммуДокумента()
	
	Объект.СуммаДокумента = Объект.Товары.Итог("Сумма") + Объект.Услуги.Итог("Сумма");
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура ВопросПользователю()  
	
	// ++ Забиров Т. Р.: Вопрос о необходимости пересчета сумм с учетом скидки
	ОтветПользователя = Ждать ВопросАсинх("Пересчитать сумму с учетом скидки?",РежимДиалогаВопрос.ДаНет);
	
	Если ОтветПользователя = КодВозвратаДиалога.Да Тогда
		ПрименитьСкидку();
	КонецЕсли;	
	// --
	
КонецПроцедуры

&НаКлиенте
Процедура ПрименитьСкидку()
	
	// ++ Забиров Т. Р.: Пересчет сумм с учетом скидки
	Для Каждого Товар Из Объект.Товары Цикл  
		Товар.Сумма = Товар.Цена * Товар.Количество * (1 - Объект.ДЗ_СогласованнаяСкидка / 100);
	КонецЦикла;
	
	Для Каждого Услуга Из Объект.Услуги Цикл  
		Услуга.Сумма = Услуга.Цена * Услуга.Количество * (1 - Объект.ДЗ_СогласованнаяСкидка / 100);
	КонецЦикла;  
	
	Объект.СуммаДокумента = Объект.Товары.Итог("Сумма") + Объект.Услуги.Итог("Сумма"); 
	// --
	
КонецПроцедуры


#Область ПодключаемыеКоманды

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
    ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
    ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
    ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#КонецОбласти
